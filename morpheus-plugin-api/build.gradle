buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
}



apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'com.morpheusdata'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenLocal()
    maven { url "https://dl.bintray.com/bertramlabs/gomorpheus/" }
    jcenter()
	mavenCentral()
}

configurations {
    provided
}

dependencies {
	implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
	implementation 'org.apache.httpcomponents:httpclient:4.5.3'
	implementation 'org.apache.httpcomponents:httpcore:4.4.5'
	implementation "org.slf4j:slf4j-parent:$slf4jVersion"
	implementation 'com.github.jknack:handlebars:4.2.0'
	implementation 'javax.servlet:javax.servlet-api:3.0.1'
	implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    compileOnly('com.morpheusdata:cypher-core:0.5.1')

	testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
	testImplementation "org.spockframework:spock-core:$spockVersion"
	testImplementation 'javax.servlet:javax.servlet-api:3.0.1'
	testImplementation 'cglib:cglib-nodep:3.3.0'
	testImplementation 'org.objenesis:objenesis:3.1'
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

//Define bintrayUser and bintrayKey in ~/.gradle/gradle.properties
bintray {
    if(project.hasProperty('bintrayUser')) {
        user = bintrayUser
        key = bintrayKey
    }
    publications = ['maven']
    pkg {
        repo = 'gomorpheus'
        userOrg = 'bertramlabs'
        name = 'morpheus-plugin-api'
        vcsUrl = 'https://github.com/gomorpheus/morpheus-core.git'
        licenses = ['Apache-2.0']
    }
}

java {
	withSourcesJar()
	withJavadocJar()
}

task morpheusJavaDoc(type: Javadoc) {
  source = sourceSets.main.allJava
  title = "Morpheus Core Docs"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'morpheus-plugin-api'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'morpheus-plugin-api'
                    description 'Morpheus Core provides the core framework for implementing extension plugins for the morpheus platform'
                    url 'https://github.com/bertramdev/morpheus-core'
                    scm {
                        url 'https://github.com/bertramdev/morpheus-core'
                        connection 'scm:https://bertramdev@github.com/gomorpheus/morpheus-core.git'
                        developerConnection 'scm:git://github.com/gomorpheus/morpheus-core.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

bintrayUpload.dependsOn build
