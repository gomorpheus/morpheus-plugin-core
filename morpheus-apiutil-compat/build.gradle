buildscript {
	repositories {
		mavenLocal()
		jcenter()
	}
	dependencies {
	}
}

plugins {
    id 'groovy'
    id 'java'
	id 'idea'
	id 'maven-publish'
}

group 'com.morpheusdata'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
	mavenLocal()
    mavenCentral()
	jcenter()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
	provided
}

dependencies {
	compileOnly project(':morpheus-plugin-api')
	compileOnly "org.codehaus.groovy:groovy-all:$groovyVersion"
	compileOnly "org.codehaus.gpars:gpars:1.2.1"
	implementation 'commons-beanutils:commons-beanutils:1.9.4'
	implementation 'commons-net:commons-net:3.7.2'

	testImplementation project(':morpheus-plugin-api')
	testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
	testImplementation project(':morpheus-test-provider')
	testImplementation 'net.bytebuddy:byte-buddy:1.9.3'
	testImplementation 'org.objenesis:objenesis:2.6'
	testImplementation "org.spockframework:spock-core:$spockVersion"
	testImplementation 'cglib:cglib-nodep:3.2.12'
}

sourceSets {
	main {
		compileClasspath += configurations.provided
	}
}

java {
	withSourcesJar()
	withJavadocJar()
}

task morpheusJavaDoc(type: Javadoc) {
	source = sourceSets.main.allJava
	title = "Morpheus API Utility"
}

task(console, dependsOn: 'classes', type: JavaExec) {
	main = 'groovy.ui.Console'
	classpath = sourceSets.main.runtimeClasspath
}

test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			artifactId 'morpheus-apiutil-compat'
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name 'morpheus-apiutil-compat'
					description 'Morpheus Api Utility Classes'
					url 'https://github.com/gomorpheus/morpheus-plugin-core'
					scm {
						url 'https://github.com/bertramdev/morpheus-core'
						connection 'scm:https://bertramdev@github.com/gomorpheus/morpheus-core.git'
						developerConnection 'scm:git://github.com/gomorpheus/morpheus-core.git'
					}
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/license/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id 'mftruso'
							name 'Mike Truso'
							email 'mftruso@gmail.com'
						}
					}
				}
			}
			from components.java
		}
	}
	repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}
